// Texture from Jason Liebig's FLICKR collection of vintage labels and wrappers:
// http://www.flickr.com/photos/jasonliebigstuff/3739263136/in/photostream/
import processing.video.*; 
PImage label;
PShape can;
float angle;
Movie video;
PShader bwShader;

void setup() {
  size(640, 360, P3D);  
  video = new Movie(this, "video.mp4");
  video.play();
  label = video;
  can = createCan(1280, 720, 32, label);
  bwShader = loadShader("bwfrag.glsl");
}

void draw() {    
  background(0);
  
  shader(bwShader);
    
  translate(width/2, height/2);
  rotateY(angle);  
  shape(can);  
  angle += 0.01;
  
  //Print the framerate of video
  println(frameRate);
}

PShape createCan(float r, float h, int detail, PImage tex) {
  textureMode(NORMAL);
  PShape sh = createShape();
  sh.beginShape(QUAD_STRIP);
  sh.noStroke();
  sh.texture(tex);
  for (int i = 0; i <= detail; i++) {
    float angle = TWO_PI / detail;
    float x = sin(i * angle);
    float z = cos(i * angle);
    float u = float(i) / detail;
    sh.normal(x, 0, z);
    sh.vertex(x * r, -h/2, z * r, u, 0);
    sh.vertex(x * r, +h/2, z * r, u, 1);    
  }
  sh.endShape(); 
  return sh;
}

void movieEvent(Movie m) {
  m.read();
}
